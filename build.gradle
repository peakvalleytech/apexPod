buildscript {
    ext.kotlin_version = '1.7.10'
    ext.core_ktx_version = '1.6.0'
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.0'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:3.0.0'
        classpath 'de.timfreiheit.resourceplaceholders:placeholders:0.4'
        classpath "gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:4.7.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.gms:google-services:4.3.15'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.6'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://jitpack.io" }
        jcenter()
    }
}

// Disable predex if requested (we can"t predex in Circle CI
// See http://tools.android.com/tech-docs/new-build-system/tips#TOC-Improving-Build-Server-performance
// and https://circleci.com/docs/android
project.ext.preDexLibs = !project.hasProperty("disablePreDex")

subprojects {
    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin" == plugin.class.name) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        } else if ("com.android.build.gradle.LibraryPlugin" == plugin.class.name) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}

project.ext {
    // AndroidX
    annotationVersion = "1.2.0"
    appcompatVersion = "1.3.1"
    coreVersion = "1.5.0"
    fragmentVersion = "1.3.6"
    mediaVersion = "1.1.0"
    preferenceVersion = "1.2.0"
    workManagerVersion = "2.8.0"
    googleMaterialVersion = "1.1.0"

    // Third-party
    commonslangVersion = "3.6"
    commonsioVersion = "2.5"
    jsoupVersion = "1.11.2"
    glideVersion = "4.8.0"
    okhttpVersion = "3.12.10"
    okioVersion = "1.17.5"
    eventbusVersion = "3.2.0"
    rxAndroidVersion = "2.1.1"
    rxJavaVersion = "2.2.2"
    iconifyVersion = "2.2.2"
    exoPlayerVersion = "2.11.8"
    audioPlayerVersion = "v2.0.0"

    // Only used for free builds. This version should be updated regularly.
    conscryptVersion = "2.4.0"

    // Google Play build
    wearableSupportVersion = "2.6.0"
    playServicesVersion = "21.3.0"
    google_play_services_version = "22.0.0"

    //Tests
    awaitilityVersion = "3.1.6"
    robotiumSoloVersion = "5.6.3"
    espressoVersion = "3.2.0"
    runnerVersion = "1.2.0"
    rulesVersion = "1.2.0"
}

apply plugin: "checkstyle"
checkstyle {
    toolVersion '8.24'
}

task checkstyle(type: Checkstyle) {
    classpath = files()
    source "${project.rootDir}"
    exclude("**/gen/**")
    exclude("**/generated/**")
}
